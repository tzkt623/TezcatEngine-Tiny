#TINY_HEAD_BEGIN
{
    str Name = Unlit/EnvMakeCube;
}
#TINY_HEAD_END

#TINY_PASS_BEGIN
{
    #TINY_CFG_BEGIN
    {
        str Name = Unlit/EnvMakeCube;
        int Version = 330;
        int OrderID = 50;
        str Queue = Prepare;
        str DepthTest = Off;
        bool ZWrite = true;
        str CullFace = Front;
    }
    #TINY_CFG_END

    #TINY_VS_BEGIN
    {
        #include "../Include/tiny_vs.tyin"

        layout (location = 0) in float3 aPos;
        out float3 myWorldPosition;

        void main()
        {
            myWorldPosition = aPos;
            gl_Position = TINY_MatrixP * TINY_MatrixV * float4(aPos, 1.0);
        }
    }
    #TINY_VS_END

    #TINY_FS_BEGIN
    {
        #include "../Include/tiny_fs.tyin"
        
        in float3 myWorldPosition;
        out float4 myFinalColor;
        uniform tex2D myTexHDR2D;

        const float2 invAtan = float2(0.1591, 0.3183);
        float2 sampleSphericalMap(float3 v)
        {
            float2 uv = float2(atan(v.z, v.x), asin(v.y));
            uv *= invAtan;
            uv += 0.5;
            return uv;
        }

        void main()
        {
            float2 uv = sampleSphericalMap(normalize(myWorldPosition));
            float3 color = texture(myTexHDR2D, uv).rgb;
            myFinalColor = float4(color, 1.0f);
        }
    }
    #TINY_FS_END
}
#TINY_PASS_END







